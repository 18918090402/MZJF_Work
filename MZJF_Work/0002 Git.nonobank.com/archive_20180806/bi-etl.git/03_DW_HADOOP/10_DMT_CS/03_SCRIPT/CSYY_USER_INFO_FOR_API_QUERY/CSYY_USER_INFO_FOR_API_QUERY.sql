set mapreduce.job.queuename=etl-dw;
set mapreduce.job.name=DMT_CS_CSYY_USER_INFO_FOR_API_QUERY_01;

INSERT OVERWRITE TABLE DMT_CS.CSYY_USER_INFO_FOR_API_QUERY
  select USER_ID,
         MOBILE_NUM,
         REAL_NAME,
         substr(regexp_extract(ID_NUM,'^([0-9X]*).*',1),-4,4) ID_NUM,
         null USER_RANK_DESC,
         CSYY_7DAYS_RECHARGE_AMT,
         CSYY_7DAYS_WITHDRAW_AMT,
         CSYY_7DAYS_ETA_AMT,
         'SYS' AS DW_CREATE_BY,
         FROM_UNIXTIME(UNIX_TIMESTAMP(),'yyyy-MM-dd HH:mm:ss') AS DW_CREATE_TIME,
         'SYS' AS DW_UPDATE_BY,
         FROM_UNIXTIME(UNIX_TIMESTAMP(),'yyyy-MM-dd HH:mm:ss') AS DW_UPDATE_TIME
    from (SELECT t1.USER_ID,
                 NVL(MAX(CASE WHEN TAG_ID=5 THEN CAST(TAG_VALUE AS STRING) END),NULL) AS MOBILE_NUM,
                 NVL(MAX(CASE WHEN TAG_ID=3 THEN CAST(TAG_VALUE AS STRING) END),NULL) AS REAL_NAME,
                 NVL(MAX(CASE WHEN TAG_ID=130 THEN CAST(TAG_VALUE AS STRING) END),NULL) AS ID_NUM, 
                 NVL(MAX(CASE WHEN TAG_ID=179 THEN CAST(TAG_VALUE AS DECIMAL(38,10)) END),NULL) AS CSYY_7DAYS_RECHARGE_AMT,
                 NVL(MAX(CASE WHEN TAG_ID=180 THEN CAST(TAG_VALUE AS DECIMAL(38,10)) END),NULL) AS CSYY_7DAYS_WITHDRAW_AMT,
                 NVL(MAX(CASE WHEN TAG_ID=181 THEN CAST(TAG_VALUE AS DECIMAL(38,10)) END),NULL) AS CSYY_7DAYS_ETA_AMT,
                 NVL(MAX(CASE WHEN TAG_ID=97 THEN CAST(TAG_VALUE AS STRING) END),NULL) AS CSYY_FIRST_INVEST_TIME,
                 NVL(MAX(CASE WHEN TAG_ID=185 THEN CAST(TAG_VALUE AS STRING) END),NULL) AS REGISTER_BIZ,
                 NVL(MAX(CASE WHEN TAG_ID=14 THEN CAST(TAG_VALUE AS INT) END),NULL) AS IS_CHEAT
            FROM IDW.USER_TAG_HIST t1
            join ods.t_user_info_hist t2
              on t1.user_id = t2.id
           WHERE TAG_ID in (3, 5, 130, 179, 180, 181, 97, 185, 14)
           GROUP BY t1.USER_ID) x
   where (CSYY_FIRST_INVEST_TIME is not null or REGISTER_BIZ = 'csyy')
     and user_id not in (12027, 920272)
     and IS_CHEAT = 0;