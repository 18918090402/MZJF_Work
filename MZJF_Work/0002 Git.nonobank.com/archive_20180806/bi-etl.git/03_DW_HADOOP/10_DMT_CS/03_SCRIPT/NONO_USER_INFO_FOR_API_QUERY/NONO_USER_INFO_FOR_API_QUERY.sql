set mapreduce.job.queuename=etl-dw;
set mapreduce.job.name=DMT_CS_NONO_USER_INFO_FOR_API_QUERY_01;

INSERT OVERWRITE TABLE DMT_CS.NONO_USER_INFO_FOR_API_QUERY
  select USER_ID,
         MOBILE_NUM,
         REAL_NAME,
         substr(regexp_extract(ID_NUM,'^([0-9X]*).*',1),-4,4) ID_NUM,
         USER_RANK_DESC,
         NONO_7DAYS_RECHARGE_AMT,
         NONO_7DAYS_WITHDRAW_AMT,
         NONO_7DAYS_ETA_AMT,
         'SYS' AS DW_CREATE_BY,
         FROM_UNIXTIME(UNIX_TIMESTAMP(),'yyyy-MM-dd HH:mm:ss') AS DW_CREATE_TIME,
         'SYS' AS DW_UPDATE_BY,
         FROM_UNIXTIME(UNIX_TIMESTAMP(),'yyyy-MM-dd HH:mm:ss') AS DW_UPDATE_TIME
    from (SELECT t1.USER_ID,
                 NVL(MAX(CASE WHEN TAG_ID=5 THEN CAST(TAG_VALUE AS STRING) END),NULL) AS MOBILE_NUM,
                 NVL(MAX(CASE WHEN TAG_ID=3 THEN CAST(TAG_VALUE AS STRING) END),NULL) AS REAL_NAME,
                 NVL(MAX(CASE WHEN TAG_ID=130 THEN CAST(TAG_VALUE AS STRING) END),NULL) AS ID_NUM, 
                 NVL(MAX(CASE WHEN TAG_ID=44 THEN CAST(TAG_VALUE AS STRING) END),NULL) AS USER_RANK_DESC,
                 NVL(MAX(CASE WHEN TAG_ID=176 THEN CAST(TAG_VALUE AS DECIMAL(38,10)) END),NULL) AS NONO_7DAYS_RECHARGE_AMT,
                 NVL(MAX(CASE WHEN TAG_ID=177 THEN CAST(TAG_VALUE AS DECIMAL(38,10)) END),NULL) AS NONO_7DAYS_WITHDRAW_AMT,
                 NVL(MAX(CASE WHEN TAG_ID=178 THEN CAST(TAG_VALUE AS DECIMAL(38,10)) END),NULL) AS NONO_7DAYS_ETA_AMT,
                 NVL(MAX(CASE WHEN TAG_ID=64 THEN CAST(TAG_VALUE AS STRING) END),NULL) AS NONO_FIRST_INVEST_TIME,
                 NVL(MAX(CASE WHEN TAG_ID=185 THEN CAST(TAG_VALUE AS STRING) END),NULL) AS REGISTER_BIZ,
                 NVL(MAX(CASE WHEN TAG_ID=14 THEN CAST(TAG_VALUE AS INT) END),NULL) AS IS_CHEAT
            FROM IDW.USER_TAG_HIST t1
            join ods.t_user_info_hist t2
              on t1.user_id = t2.id
           WHERE TAG_ID in (3, 5, 130, 44, 176, 177, 178, 64, 185, 14)
           GROUP BY t1.USER_ID) x
   where (NONO_FIRST_INVEST_TIME is not null or REGISTER_BIZ = 'nono')
     and user_id not in (12027, 920272)
     and IS_CHEAT = 0;