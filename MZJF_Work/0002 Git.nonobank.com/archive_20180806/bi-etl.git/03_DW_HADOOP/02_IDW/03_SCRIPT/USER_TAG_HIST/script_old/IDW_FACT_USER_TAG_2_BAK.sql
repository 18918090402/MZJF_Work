set mapreduce.job.queuename=etl-dw;

set mapreduce.job.name=IDW_FACT_USER_TAG_2_01;

set hive.groupby.skewindata = true ;
set hive.map.aggr = true ; 
set hive.enforce.bucketing = true;

set hive.exec.parallel=true;
set hive.auto.convert.join=true;
set hive.exec.dynamic.partition=true;
set hive.exec.dynamic.partition.mode=nonstrict;
set hive.exec.max.dynamic.partitions.pernode=100000000;
set hive.exec.max.dynamic.partitions=100000000;
set hive.exec.max.created.files=100000000;

FROM 
(
  SELECT /*+ mapjoin(r1,r2,r3,r4,r5)*/
   uid.USER_ID,
   uid.ENGLISH_NAME AS ENGLISH_NAME,
   if(uid.AGE > 0 and uid.AGE < 100, uid.AGE, null) AS AGE,
   uid.CONSTELLATION AS CONSTELLATION,
   uid.QQ AS QQ,
   uid.CUR_PHONE AS CURRENT_MOBILE_NO,
   uid.REGISTER_PROVINCE AS HJ_PROVINCE,
   r1.name AS HJ_PROVINCE_NAME,
   uid.REGISTER_CITY AS HJ_CITY,
   r2.name AS HJ_CITY_NAME,
   uid.REGISTER_ADDRESS AS HJ_ADDRESS,
   case
     when uid.CURRENT_PROVINCE regexp '^[0-9]*$' then
      uid.CURRENT_PROVINCE
   end AS CURRENT_PROVINCE,
   r3.name AS CURRENT_PROVINCE_NAME,
   case
     when uid.CURRENT_CITY regexp '^[0-9]*$' then
      uid.CURRENT_CITY
   end AS CURRENT_CITY,
   r4.name AS CURRENT_CITY_NAME,
   uid.BIRTH_PROVINCE AS BIRTH_PROVINCE,
   r5.name AS BIRTH_PROVINCE_NAME,
   uid.CURRENT_ADDRESS AS CONTACT_ADDRESS,
   uid.MARRIAGE_STATUS AS MARRIAGE_STATUS,
   CASE uid.MARRIAGE_STATUS
     WHEN 1 THEN
      '已婚'
     WHEN 2 THEN
      '未婚'
     WHEN 3 THEN
      '已婚已育'
     WHEN 4 THEN
      '已婚未育'
     WHEN 5 THEN
      '未婚已育'
     WHEN 6 THEN
      '丧偶'
     WHEN 7 THEN
      '离婚'
     ELSE
      '未知'
   END AS MARRIAGE_STATUS_DESC,
   uid.IS_HAVE_PASSPORT AS IS_HAVE_PASSPORT,
   uid.IS_SOCIAL_SECURITY AS IS_SOCIAL_SECURITY,
   uid.IS_DRIVING_LICENSE AS IS_DRIVING_LICENSE,
   uid.HOUSE_STATUS AS HOUSE_STATUS,
   uid.EDUCATION AS EDUCATION
    FROM ODS.T_USER_INFO_DETAIL_HIST uid
    LEFT JOIN ods.t_region_hist r1
      on uid.REGISTER_PROVINCE = cast(r1.code as string)
    LEFT JOIN ods.t_region_hist r2
      on uid.REGISTER_CITY = cast(r2.code as string)
    LEFT JOIN ods.t_region_hist r3
      on uid.CURRENT_PROVINCE = cast(r3.code as string)
    LEFT JOIN ods.t_region_hist r4
      on uid.CURRENT_CITY = cast(r4.code as string)
    LEFT JOIN ods.t_region_hist r5
      on uid.BIRTH_PROVINCE = cast(r5.code as string)
) OUTPUT
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=19)SELECT  USER_ID,ENGLISH_NAME, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=21)SELECT  USER_ID,AGE, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=22)SELECT  USER_ID,CONSTELLATION, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=23)SELECT  USER_ID,QQ, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=24)SELECT  USER_ID,CURRENT_MOBILE_NO, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=25)SELECT  USER_ID,HJ_PROVINCE, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=26)SELECT  USER_ID,HJ_CITY, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=27)SELECT  USER_ID,HJ_ADDRESS, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=28)SELECT  USER_ID,CURRENT_PROVINCE, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=29)SELECT  USER_ID,CURRENT_CITY, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=30)SELECT  USER_ID,BIRTH_PROVINCE, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=31)SELECT  USER_ID,CONTACT_ADDRESS, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=32)SELECT  USER_ID,MARRIAGE_STATUS, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=33)SELECT  USER_ID,MARRIAGE_STATUS_DESC, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=34)SELECT  USER_ID,IS_HAVE_PASSPORT, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=35)SELECT  USER_ID,IS_SOCIAL_SECURITY, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=36)SELECT  USER_ID,IS_DRIVING_LICENSE, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=37)SELECT  USER_ID,HOUSE_STATUS, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=38)SELECT  USER_ID,EDUCATION, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=87)SELECT  USER_ID,HJ_PROVINCE_NAME, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=88)SELECT  USER_ID,HJ_CITY_NAME, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=89)SELECT  USER_ID,CURRENT_PROVINCE_NAME, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=90)SELECT  USER_ID,CURRENT_CITY_NAME, 4
INSERT OVERWRITE TABLE IDW.USER_TAG_TMP_01 PARTITION(TAG_ID=91)SELECT  USER_ID,BIRTH_PROVINCE_NAME, 4
;
-- -------------------------------------------------------------------------------------------------
set mapreduce.job.queuename=etl-dw;

set mapreduce.job.name=IDW_FACT_USER_TAG_2_02;

set hive.exec.parallel=true ;
set hive.groupby.skewindata = true ;
set hive.map.aggr = true ; 
set hive.auto.convert.join=true;
set hive.enforce.bucketing = true;

set hive.exec.dynamic.partition=true;
set hive.exec.dynamic.partition.mode=nonstrict;
set hive.exec.max.dynamic.partitions.pernode=100000000;
set hive.exec.max.dynamic.partitions=100000000;
set hive.exec.max.created.files=100000000;

INSERT OVERWRITE TABLE   IDW.USER_TAG_HIST  PARTITION(TAG_ID,HASH_PARTITION_ID)
SELECT   USER_ID       
        ,TAG_VALUE
        ,'SYS' AS DW_CREATE_BY
        ,FROM_UNIXTIME(UNIX_TIMESTAMP(),'yyyy-MM-dd HH:mm:ss') AS DW_CREATE_TIME
        ,'SYS' AS DW_UPDATE_BY
        ,FROM_UNIXTIME(UNIX_TIMESTAMP(),'yyyy-MM-dd HH:mm:ss') AS DW_UPDATE_TIME
        ,TAG_ID
        ,CAST(PMOD(USER_ID,10) AS INT) HASH_PARTITION_ID 
FROM    IDW.USER_TAG_TMP_01  
WHERE ((TAG_ID >=21 AND TAG_ID <=38) OR (TAG_ID >=87 AND TAG_ID <=91) OR TAG_ID = 19)
  AND USER_ID   IS NOT NULL 
  AND TAG_VALUE IS NOT NULL 
  ;